apply from: "${rootDir}/gradle/mwe2-workflows.gradle"

dependencies {
	compile project(':org.eclipse.xtext.builder.standalone')
	compile project(':org.eclipse.xtext.ecore')
	compile "org.eclipse.xtext:org.eclipse.xtext.testing:$versions.xtext"
	compile 'junit:junit'
}

// Include the mwe2 and xtext files from the test source set when executing the workflow
sourceSets.mwe2.runtimeClasspath += processTestResources.outputs.files

task generateTestLanguages(type: XtextGeneratorTask) {
	workflow = file('src/org/eclipse/xtext/builder/tests/GenerateBuilderTestLanguages.mwe2')
	outputs.dir 'src-gen'
	dependsOn 'processTestResources'
}

tasks.getByName('compileTestJava') {
	mustRunAfter 'generateTestLanguages'
}

tasks.getByName('compileJava') {
	mustRunAfter 'generateTestLanguages'
}

if (tasks.findByName('generateTestXtext')) {
	tasks.getByName('generateTestXtext') {
		mustRunAfter 'generateTestLanguages'
	}
}

if (tasks.findByName('generateXtext')) {
	tasks.getByName('generateXtext') {
		mustRunAfter 'generateTestLanguages'
	}
}

