/*******************************************************************************
 * Copyright (c) 2010, 2017 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
module org.eclipse.xtext.common.types.xtext.ui.GenerateTypesTestLanguages

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*

var rootPath = ".."

Workflow {
	bean = StandaloneSetup {
		platformUri = rootPath
	}
	
    component = XtextGenerator {
    	configuration = {
			project = model.project.StandardProjectConfig {
				baseName = "org.eclipse.xtext.common.types.tests"
				rootPath = rootPath
				
				runtimeTest = {
					enabled = true
					root = "${rootPath}/org.eclipse.xtext.common.types.tests"
				}
			}
			code = {
				encoding = 'ISO-8859-1'
				lineDelimiter = "\n"
				preferXtendStubs = false
			}
		}
		
		language = { 
			grammarUri = "classpath:/org/eclipse/xtext/common/types/xtext/ui/ContentAssistTestLanguage.xtext"
			fileExtensions = "typesAssistTest"
    		referencedResource = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
			
			fragment = @CommonTestLanguagesFragments {}
            fragment = generator.GeneratorFragment2 {
            	generateStub = false
            }
		}
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/common/types/xtext/ui/RefactoringTestLanguage.xtext"
			fileExtensions = "typesRefactoring"
    		referencedResource = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
    		
			fragment = @CommonTestLanguagesFragments {}
			fragment = junit.JUnitFragment {
				useDeprecatedClasses = false
				generateStub = false
			}
		}
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/common/types/xtext/ui/RefactoringTestLanguage1.xtext"
			fileExtensions = "typesRefactoring1"
    		referencedResource = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
    		
			fragment = @CommonTestLanguagesFragments {}
			fragment = ui.refactoring.RefactorElementNameFragment2 {}
		}
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/common/types/xtext/ui/RefactoringTestLanguage2.xtext"
			fileExtensions = "typesRefactoring2"
    		referencedResource = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
    		
			fragment = @CommonTestLanguagesFragments {}
			fragment = ui.refactoring.RefactorElementNameFragment2 {
				useJdtRefactoring = true
			}	
		}
	}
}
