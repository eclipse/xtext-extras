apply from: "${rootDir}/gradle/longrunning-tests.gradle"
apply from: "${rootDir}/gradle/mwe2-workflows.gradle"

dependencies {
	compile project(':org.eclipse.xtext.common.types')
	compile project(':org.eclipse.xtext.xbase.testdata')
	compile "org.eclipse.xtext:org.eclipse.xtext.testing:$versions.xtext"
	compile 'org.eclipse.platform:org.eclipse.equinox.common'
	compile 'org.eclipse.platform:org.eclipse.core.runtime'
	compile 'org.eclipse.jdt:org.eclipse.jdt.core'
	compile 'org.eclipse.jdt:org.eclipse.jdt.compiler.apt'
	compile 'org.eclipse.jdt:org.eclipse.jdt.compiler.tool'
	compile 'com.google.errorprone:error_prone_annotations'
	compile 'junit:junit'
	compile 'javax.annotation:javax.annotation-api'
	compile 'com.google.code.findbugs:jsr305'
}

sourceSets.test.java {
	srcDir 'testdata'
	srcDir 'tests'
}

sourceSets.test.resources {
	srcDir 'tests'
}

// Include the mwe2 and xtext files from the test source set when executing the workflow
sourceSets.mwe2.runtimeClasspath += processTestResources.outputs.files

task generateTestLanguages(type: XtextGeneratorTask) {
	workflow = file('src/org/eclipse/xtext/common/types/xtext/ui/GenerateTypesTestLanguages.mwe2')
	outputs.dir 'src-gen'
	dependsOn 'processTestResources'
}

tasks.getByName('compileTestJava') {
	mustRunAfter 'generateTestLanguages'
}

tasks.getByName('compileJava') {
	mustRunAfter 'generateTestLanguages'
}

if (tasks.findByName('generateTestXtext')) {
	tasks.getByName('generateTestXtext') {
		mustRunAfter 'generateTestLanguages'
	}
}

if (tasks.findByName('generateXtext')) {
	tasks.getByName('generateXtext') {
		mustRunAfter 'generateTestLanguages'
	}
}
