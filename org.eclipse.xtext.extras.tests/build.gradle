apply from: "${rootDir}/gradle/mwe2-workflows.gradle"

dependencies {
	compile project(':org.eclipse.xtext.generator')
	compile project(':org.eclipse.xtext.common.types')
	compile project(':org.eclipse.xtext.xbase')
	compile "org.eclipse.xtext:org.eclipse.xtext.testing:$versions.xtext"
	compile 'junit:junit'
	optional files('lib/simple.jar')
	// The MWE2 workflow depends on emf-gen, so we have to include it in the test dependencies
	testCompile sourceSets.mwe2.output
}

sourceSets.test {
	java.srcDirs = ['src', 'src-gen', 'deprecated']
	resources.srcDirs = ['src', 'src-gen', 'deprecated']
	if (findProperty('compileXtend') != 'true') {
		java.srcDir 'xtend-gen'
	}
}

sourceSets.mwe2 {
	java.srcDirs = ['generator/src', 'emf-gen']
	runtimeClasspath += processTestResources.outputs.files
}
jar.from sourceSets.mwe2.output
sourcesJar.from sourceSets.mwe2.allSource

task generateTestLanguages(type: XtextGeneratorTask) {
	workflow = file('src/org/eclipse/xtext/GenerateAllTestLanguages.mwe2')
	outputs.dir 'src-gen'
	dependsOn 'processTestResources'
}

tasks.getByName('compileTestJava') {
	mustRunAfter 'generateTestLanguages'
}

tasks.getByName('compileJava') {
	mustRunAfter 'generateTestLanguages'
}

if (tasks.findByName('generateTestXtext')) {
	tasks.getByName('generateTestXtext') {
		mustRunAfter 'generateTestLanguages'
	}
}

if (tasks.findByName('generateXtext')) {
	tasks.getByName('generateXtext') {
		mustRunAfter 'generateTestLanguages'
	}
}
